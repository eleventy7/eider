/*
 * Copyright 2019 Shaun Laurens
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'maven'
    id 'idea'
    id 'checkstyle'
    id 'com.github.spotbugs' version '2.0.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0' apply false
}

defaultTasks 'clean', 'build', 'test'

def AERON_VERSION = '1.20.0'
def AGRONA_VERSION = '1.0.3'
def CHECKSTYLE_VERSION = '8.22'
def JUPITER_VERSION = '5.5.0'
def MOCKITO_VERSION = '3.0.0'
def SBE_VERSION = '1.12.8'

def eiderGroup = 'io.eider'
def eiderVersion = '0.1-SNAPSHOT'
def eiderJavaVersion = JavaVersion.VERSION_11

configurations {
    shadow
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'

    group eiderGroup
    version eiderVersion

    compileJava {
        sourceCompatibility = 11
        targetCompatibility = 11
        options.compilerArgs << '-XDignore.symbol.file' // Suppress warnings about using Unsafe
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    compileTestJava {
        sourceCompatibility = eiderJavaVersion
        targetCompatibility = eiderJavaVersion
        options.encoding = 'UTF-8'
        options.deprecation = true
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'io.aeron', name: 'aeron-all', version: AERON_VERSION
        implementation group: 'org.agrona', name: 'agrona', version: AGRONA_VERSION
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: CHECKSTYLE_VERSION

        testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: JUPITER_VERSION

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: JUPITER_VERSION
        testImplementation group: 'org.mockito', name: 'mockito-core', version: MOCKITO_VERSION
    }

    tasks.withType(Checkstyle) {
        checkstyle {
            configFile = "${rootDir}/config/eider_checks.xml" as File
            configProperties = ["suppressionFile": "${rootDir}/config/suppressions.xml"]
            toolVersion = CHECKSTYLE_VERSION
        }
    }

    test {
        useJUnitPlatform()
        jvmArgs('--add-opens', 'java.base/java.lang.reflect=ALL-UNNAMED')
        jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')

        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

project('eider-core') {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    dependencies {
        compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.12'
    }

    spotbugs {
        excludeFilter = file("$rootDir/config/spotbugs_excludes.xml")
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    def generatedDir = file("${buildDir}/generated-src")
    sourceSets {
        generated.java.srcDir generatedDir
    }

    dependencies {
        compile "org.agrona:agrona:${AGRONA_VERSION}"
        compile "uk.co.real-logic:sbe-tool:${SBE_VERSION}"
        testCompile sourceSets.generated.output
    }

    compileGeneratedJava {
        dependsOn 'generateMessages'
        sourceCompatibility = eiderJavaVersion
        targetCompatibility = eiderJavaVersion
        options.deprecation = true
        classpath += sourceSets.main.runtimeClasspath
    }

    task generateMessages(type: JavaExec) {
        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = sourceSets.main.runtimeClasspath
        systemProperties('sbe.output.dir': generatedDir,
                'sbe.target.language': 'Java',
                'sbe.validation.stop.on.error': 'true',
                'sbe.validation.xsd': 'src/main/resources/sbe/sbe.xsd')
        args = [
                'src/main/resources/io/eider/protocol/messages.xml'
        ]
    }

    jar.dependsOn compileGeneratedJava
    jar.enabled = true

    jar {
        manifest.attributes(
                'Implementation-Title': 'Eider',
                'Implementation-Version': "${eiderVersion}",
                'Implementation-Vendor': 'Shaun Laurens',
                'Automatic-Module-Name': 'io.eider.eidercore'
        )
        from(sourceSets.generated.output) {
            include '**/*.class'
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

wrapper {
    gradleVersion = '5.5.1'
    distributionType = 'ALL'
}